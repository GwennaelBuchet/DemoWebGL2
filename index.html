<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <title>Title</title>
</head>
<body>

<canvas id="scene" width="800px" height="600px" style="background: black">
    It should be a very cool 3D animation here, but your dinosaur does not support canvas tag. <br/>
    It's time to come back to the present days... :(
</canvas>

<div id="noContextLayer" style="display: none">
    <h2>Ouch, your dinosaur does not support WebGL2 yet. </h2>
    <h3>It's time to come back to the present days...</h3>
</div>


<script id="phong-vshader" type="x-shader/x-vertex">#version 300 es

layout(location = 0) in vec3 aVertexPosition;
layout(location = 1) in vec3 aVertexNormal;
layout(location = 2) in vec2 aTextureCoord;

uniform mat4 uNormalMatrix, uModelViewMatrix, uProjectionMatrix, uWorldMatrix;

out vec3 normalInterp;
out vec3 vertPos;
out vec2 vTextureCoord;

void main()
{
    vec4 vertPos4 = uModelViewMatrix * vec4(aVertexPosition, 1.0);
    vertPos = vec3(vertPos4) / vertPos4.w;
    normalInterp = vec3(uNormalMatrix * vec4(aVertexNormal, 0.0));

    vTextureCoord = aTextureCoord;

    gl_Position = uProjectionMatrix * vertPos4;
}

</script>

<script id="phong-fshader" type="x-shader/x-fragment">#version 300 es
precision mediump float;
out vec4 outColor;

uniform vec3 uLightPos;
uniform float uShininess;
uniform float uKa;
uniform float uKd;
uniform float uKs;
uniform vec3 uAmbientColor;
uniform vec3 uDiffuseColor;
uniform vec3 uSpecularColor;

uniform sampler2D uSampler;
uniform bool uUseTexture;
uniform bool uUseLight;

in vec3 normalInterp;
in vec3 vertPos;
in vec2 vTextureCoord;

void main()
{
    vec3 texelColor;
    if (uUseTexture == true) {
        texelColor = vec3(texture(uSampler, vTextureCoord));
    }
    else {
        texelColor = uDiffuseColor;
    }

    if (uUseLight) {
        vec3 N = normalize(normalInterp);
        vec3 L = normalize(uLightPos - vertPos);

        // Lambert's cosine law
        float lambertian = max(dot(N, L), 0.0);
        float specular = 0.0;
        if(lambertian > 0.0) {
            vec3 R = reflect(-L, N);      // Reflected light vector
            vec3 V = normalize(-vertPos); // Vector to viewer
            // Compute the specular term
            float specAngle = max(dot(R, V), 0.0);
            specular = pow(specAngle, uShininess);
        }

        outColor = vec4(uKa * uAmbientColor +
                        uKd * lambertian * texelColor +
                        uKs * specular * uSpecularColor, 1.0);
    }
    else {
        outColor = vec4(texelColor, 1.0);
    }
}

</script>

<script id="toon-vshader" type="x-shader/x-vertex">#version 300 es

layout(location = 0) in vec3 aVertexPosition;
layout(location = 1) in vec3 aVertexNormal;
layout(location = 2) in vec2 aTextureCoord;

uniform mat4 uNormalMatrix, uModelViewMatrix, uProjectionMatrix;
uniform vec3 uCameraPosition;

out vec3 normalInterp;
out vec3 vertPos;
out vec2 vTextureCoord;
out vec4 eye;

void main()
{
    vec4 vertPos4 = uModelViewMatrix * vec4(aVertexPosition, 1.0);
    vertPos = vec3(vertPos4) / vertPos4.w;
    normalInterp = vec3(uNormalMatrix * vec4(aVertexNormal, 0.0));

    mat4 eyeMatrix = inverse(uModelViewMatrix);
    eye = normalize(eyeMatrix * vec4(aVertexPosition, 1.0));

    vTextureCoord = aTextureCoord;

    gl_Position = uProjectionMatrix * vertPos4;
}

</script>

<script id="toon-fshader" type="x-shader/x-fragment">#version 300 es
precision mediump float;
out vec4 outColor;

uniform vec3 uLightPos;
uniform float uShininess;
uniform float uKa;
uniform float uKd;
uniform float uKs;
uniform vec3 uAmbientColor;
uniform vec3 uDiffuseColor;
uniform vec3 uSpecularColor;

uniform sampler2D uSampler;
uniform bool uUseTexture;
uniform bool uUseLight;

in vec3 normalInterp;
in vec3 vertPos;
in vec2 vTextureCoord;
in vec4 eye;

void main()
{
    vec3 texelColor;
    if (uUseTexture == true) {
        texelColor = vec3(texture(uSampler, vTextureCoord));
    }
    else {
        texelColor = uDiffuseColor;
    }


    if (uUseLight) {
        vec3 N = normalize(normalInterp);
        vec3 L = normalize(uLightPos - vertPos);

        // Lambert's cosine law
        float lambertian = max(dot(N, L), 0.0);
        vec4 color;
        float fraction = 1.0;

        if (lambertian > pow(0.95, fraction)) {
            color = vec4(vec3(1.0), 1.0);
        }
        else if (lambertian > pow(0.5, fraction)) {
            color = vec4(vec3(0.7), 1.0);
        }
        /*else if (lambertian > pow(0.05, fraction)) {
            color = vec4(vec3(0.35), 1.0);
        }*/
        else {
            color = vec4(vec3(0.5), 1.0);
        }

        float edgeDetection = (dot(vec3(eye), N) > 0.15) ? 1.0 : 0.0;

        outColor = edgeDetection * color * vec4(uKa * uAmbientColor + uKd * lambertian * texelColor, 1.0);

    }
    else {
        outColor = vec4(texelColor, 1.0);
    }
}

</script>

<script id="toon2-vshader" type="x-shader/x-vertex">#version 300 es

layout(location = 0) in vec3 aVertexPosition;
layout(location = 1) in vec3 aVertexNormal;
layout(location = 2) in vec2 aTextureCoord;

uniform mat4 uNormalMatrix, uModelViewMatrix, uProjectionMatrix, uCameraMatrix;

//send them to fragment shader
out vec3 world_pos;
out vec3 world_normal;

void main()
{
    //convert in world coords
    world_pos = mat3(uModelViewMatrix) * aVertexPosition;//careful here
    world_normal = normalize(mat3(uModelViewMatrix) * aVertexNormal);
    gl_Position = uProjectionMatrix * uModelViewMatrix * vec4(aVertexPosition, 1);
}

</script>

<script id="toon2-fshader" type="x-shader/x-fragment">#version 300 es
precision mediump float;
out vec4 out_color;

uniform vec3 uLightPos;
uniform vec3 uCameraPosition;
uniform float uShininess;
uniform float uKd;
uniform float uKs;
uniform vec3 uAmbientColor;
uniform vec3 uDiffuseColor;

in vec3 world_pos;
in vec3 world_normal;

//number of levels
//for diffuse color
const float levels = 5.0;
const float scaleFactor = 1.0 / levels;

void main()
{
    vec3 L = normalize(uLightPos - world_pos);
    vec3 V = normalize(uCameraPosition - world_pos);

    float diffuse = max(0.0, dot(L, world_normal));
    vec3 diffuseColor = uDiffuseColor * uKd * floor(diffuse * levels) * scaleFactor;

    vec3 H = normalize(L + V);

    float specular = 0.0;
    if (dot(L, world_normal) > 0.0)
    {
       specular = uKs * pow( max(0.0, dot( H, world_normal)), uShininess);
    }

    //limit specular
    float specMask = (pow(dot(H, world_normal), uShininess) > 0.4) ? 1.0 : 0.0;

    float edgeDetection = (dot(V, world_normal) > 0.3) ? 1.0 : 0.0;

    vec3 color = edgeDetection * (uAmbientColor + diffuseColor + specular * specMask);
    //vec3 color = vec3(1.0, 1.0, 1.0);

    out_color = vec4(color, 1.0);
}

</script>

<script id="basic-vshader" type="x-shader/x-vertex">#version 300 es

in vec4 aVertexPosition;
in vec3 aVertexNormal;
in vec2 aTextureCoord;

uniform mat4 uNormalMatrix;
uniform mat4 uModelViewMatrix;
uniform mat4 uProjectionMatrix;

uniform bool uUseLight;
uniform vec3 uAmbientColor;
uniform vec3 uLightDirection;
uniform vec3 uLightColor;

out vec2 vTextureCoord;
out vec3 vLighting;

void main(void) {
  gl_Position = uProjectionMatrix * uModelViewMatrix * aVertexPosition;
  vTextureCoord = aTextureCoord;

    if (!uUseLight) {
        vLighting = vec3(1.0, 1.0, 1.0);
    }
    else {
        highp vec4 transformedNormal = uNormalMatrix * vec4(aVertexNormal, 1.0);
        highp float directional = max(dot(transformedNormal.xyz, uLightDirection), 0.0);

        vLighting = uAmbientColor + (uLightColor * directional);
  }
}








</script>

<script id="basic-fshader" type="x-shader/x-fragment">#version 300 es
precision mediump float;

in vec2 vTextureCoord;
in vec3 vLighting;

uniform sampler2D uSampler;
uniform bool uUseTexture;

out vec4 outColor;

void main(void) {
    vec4 texelColor;
     if (uUseTexture == true) {
        texelColor = texture(uSampler, vTextureCoord);
     }
     else {
        texelColor = vec4(0.9, 1.0, 0.8, 1.0);
     }

    outColor = vec4(texelColor.rgb * vLighting, texelColor.a);
}








</script>

<script src="gl-matrix.js"></script>
<!--script src="objLoader.js"></script-->
<script src="webgl-obj-loader.js"></script>
<script src="zenibar.js"></script>

</body>
</html>